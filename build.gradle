plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.7'
}

version = '0.0.0'
sourceCompatibility = 11
targetCompatibility = 11

test {
    useJUnitPlatform()
    minHeapSize = "128m"
    maxHeapSize = "1024m"

    // Headless test execution (true by default)
    if (project.hasProperty("headless") ? project.headless : true) {
        jvmArgs "-Dtestfx.robot=glass"
        jvmArgs "-Dtestfx.headless=true"
        jvmArgs "-Dprism.order=sw"
        jvmArgs "-Dprism.text=t2k"
    }

    beforeTest {
        descriptor -> logger.lifecycle("Running test: " + descriptor)
    }
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml', 'javafx.web']
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "de.jensd:fontawesomefx-fontawesome:4.7.0-11"
    implementation "com.github.penemue:keap:0.3.0"
    implementation group: 'org.json', name: 'json', version: '20170516'
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    implementation group: 'de.adorsys.multibanking', name: 'finapi-adapter', version: '3.0.16'

    implementation 'org.kordamp.ikonli:ikonli-javafx:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-fontawesome5-pack:12.2.0'
    implementation 'org.kordamp.ikonli:ikonli-material-pack:12.2.0'
    implementation 'org.codehaus.griffon:griffon-javafx:2.15.1'
    implementation 'org.controlsfx:controlsfx:11.1.1'
    implementation 'org.webjars.bowergithub.jsonpath-plus:jsonpath:4.0.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testImplementation "org.testfx:testfx-junit5:4.0.15-alpha"
    testRuntimeOnly "org.testfx:openjfx-monocle:jdk-11+26"
}

task(runCLI, dependsOn: 'classes', type: JavaExec) {
    main = 'app.foodapp.FoodAppCLI'
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    args "list"
    args "today"
}

application {
    mainClassName = 'app.foodapp.FoodApp'
    applicationDefaultJvmArgs = [
            '--add-opens=javafx.base/com.sun.javafx.event=ALL-UNNAMED'
    ]
}

javadoc {
    options.with {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}
